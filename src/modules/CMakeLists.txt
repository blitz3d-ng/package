include_directories (SYSTEM .)

# some useful macros...
macro(bb_start_module name)
  set(MODULE_ID ${name})
  set(DEPENDS_ON )
  set(SOURCES )
  set(LIBS )
  set(SYSTEM_LIBS )
endmacro()

macro(bb_end_module)
  add_library (bb.${MODULE_ID} OBJECT ${SOURCES})
  target_link_libraries (bb.${MODULE_ID} ${DEPENDS_ON} ${LIBS} ${SYSTEM_LIBS})

  if(BB_COVERAGE)
    set_target_properties(bb.${MODULE_ID} PROPERTIES COMPILE_FLAGS ${BB_COVERAGE})
  endif()

  set(IFACE ${TOOLCHAIN_PATH}/cfg/${MODULE_ID}.i)

  file(WRITE  ${IFACE} "ID: bb.${MODULE_ID}\n")
  file(APPEND ${IFACE} "MODULES: ${DEPENDS_ON}\n")
  file(APPEND ${IFACE} "LIBS: ${LIBS}\n")
  file(APPEND ${IFACE} "SYSTEM_LIBS: ${SYSTEM_LIBS}\n")

  if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/commands.decls")
    file(READ "${CMAKE_CURRENT_LIST_DIR}/commands.decls" COMMANDS)
    string(REPLACE ";" "--" COMMANDS "${COMMANDS}")
    string(REPLACE "\n" ";" COMMANDS "${COMMANDS}")

    macro(link str)
      set(LINK_SRC "${LINK_SRC}${str}\n")
    endmacro()

    macro(head str)
      set(HEADER_SRC "${HEADER_SRC}${str}\n")
    endmacro()

    function(file_write_changes path src)
      set(changed true)
      if(EXISTS ${path})
        file(READ ${path} prev_src)
        if("${src}" STREQUAL "${prev_src}")
          set(changed false)
        endif()
      endif()

      if(changed)
        file(WRITE ${path} "${src}")
      endif()
    endfunction()

    string(REPLACE "-" "_" MODULE_IDENT ${MODULE_ID})
    string(REPLACE "." "_" MODULE_IDENT ${MODULE_IDENT})
    string(TOUPPER "BB_${MODULE_IDENT}_COMMANDS_H" HEADER_DEF)

    head("#ifndef ${HEADER_DEF}")
    head("#define ${HEADER_DEF}\n")

    head("#include <bb/blitz/module.h>")
    head("#include <bb/${MODULE_ID}/${MODULE_ID}.h>")
    head("")
    head("#ifdef __cplusplus")
    head("extern \"C\" {")
    head("#endif")
    head("")
    head("// AUTOGENERATED. DO NOT EDIT.")
    head("// RUN `make` TO UPDATE.")

    link("// AUTOGENERATED. DO NOT EDIT.")
    link("// RUN `make` TO UPDATE.")
    link("")
    link("#include <bb/blitz/module.h>")
    link("#include <bb/${MODULE_ID}/${MODULE_ID}.h>")
    link("")
    link("BBMODULE_LINK( ${MODULE_IDENT} ){")

    set(REGEXP_IDENT "([a-zA-Z_][_a-zA-Z_0-9]*)")
    set(REGEXP_TYPE "[#%$]|\.([a-zA-Z_][a-zA-Z_0-9]*)")

    set(REGEXP_COM "^(${REGEXP_IDENT})(${REGEXP_TYPE})?\\((.*)\\):\"(${REGEXP_IDENT})\"$")
    set(REGEXP_SYM "^(${REGEXP_IDENT}):\"(&?${REGEXP_IDENT})\"$")

    set(COMMAND_IDENTS "")

    foreach(DECL IN LISTS COMMANDS)
      string(STRIP "${DECL}" DECL)

      if("${DECL}" MATCHES "^--")
        string(REGEX REPLACE "^--" "" COMMENT "${DECL}")
        head("\n//${COMMENT}")
      elseif("${DECL}" STREQUAL "")
        # don't care...
      elseif("${DECL}" MATCHES "${REGEXP_SYM}")
        file(APPEND ${IFACE} "SYM: ${DECL}\n")

        set(IDENT  ${CMAKE_MATCH_1})
        set(SYMBOL ${CMAKE_MATCH_3})
        link("\trtSym( \"${IDENT}\",${SYMBOL} );")
      elseif("${DECL}" MATCHES "${REGEXP_COM}")
        set(IDENT  ${CMAKE_MATCH_1})
        if(NOT ${CMAKE_MATCH_4} STREQUAL "")
          set(RET ${CMAKE_MATCH_4})
        else()
          set(RET ${CMAKE_MATCH_3})
        endif()
        set(PARAMS ${CMAKE_MATCH_5})
        set(SYMBOL ${CMAKE_MATCH_6})

        string(STRIP "${PARAMS}" PARAMS)
        string(REPLACE "," ";" PARAMS "${PARAMS}")

        list(APPEND COMMAND_SYMBOLS ${SYMBOL})

        if("${RET}" STREQUAL "")
          set(CTYPE "void")
          set(BBTYPE "")
        elseif("${RET}" STREQUAL "%")
          set(CTYPE "bb_int_t")
          set(BBTYPE "%")
        elseif("${RET}" STREQUAL "#")
          set(CTYPE "bb_float_t")
          set(BBTYPE "#")
        elseif("${RET}" STREQUAL "$")
          set(CTYPE "BBStr *")
          set(BBTYPE "$")
        else()
          set(CTYPE "${RET} *")
          set(BBTYPE "%")
        endif()

        set(RTSYM "${BBTYPE}${IDENT}")

        set(ARGS_LIST "")
        foreach(PARAM IN LISTS PARAMS)
          set(REGEXP_ARG "(${REGEXP_IDENT})(${REGEXP_TYPE})(=(.*))?")


          if(NOT "${PARAM}" MATCHES ${REGEXP_ARG})
            message(FATAL_ERROR "Cannot parse ${PARAM} for ${DECL}")
          endif()

          set(PARAM_IDENT ${CMAKE_MATCH_1})
          if(NOT ${CMAKE_MATCH_4} STREQUAL "")
            set(PARAM_RET ${CMAKE_MATCH_4})
          else()
            set(PARAM_RET ${CMAKE_MATCH_3})
          endif()
          set(PARAM_DEF   ${CMAKE_MATCH_5})

          if("${PARAM_IDENT}" STREQUAL "float" OR "${PARAM_IDENT}" STREQUAL "int" OR "${PARAM_IDENT}" STREQUAL "short")
            set(PARAM_IDENT "${PARAM_IDENT}_")
          endif()

          set(PARAM_CTYPE "")
          set(PARAM_BBTYPE ${PARAM_RET})
          if("${PARAM_RET}" STREQUAL "%")
            set(PARAM_CTYPE "bb_int_t ")
          elseif("${PARAM_RET}" STREQUAL "#")
            set(PARAM_CTYPE "bb_float_t ")
          elseif("${PARAM_RET}" STREQUAL "$")
            set(PARAM_CTYPE "BBStr *")
          else()
            set(PARAM_CTYPE "${PARAM_RET} *")
            set(PARAM_BBTYPE "%")
          endif()

          string(REPLACE "\"" "\\\\\"" PARAM_DEF "${PARAM_DEF}")

          list(APPEND ARGS_LIST "${PARAM_CTYPE}${PARAM_IDENT}")

          set(RTSYM "${RTSYM}${PARAM_BBTYPE}${PARAM_IDENT}${PARAM_DEF}")
        endforeach()

        list(JOIN ARGS_LIST "," ARGS_STR)

        link("\trtSym( \"${RTSYM}\",${SYMBOL} );")

        file(APPEND ${IFACE} "FUNC: ${RTSYM};${SYMBOL}\n")

        head("${CTYPE} BBCALL ${SYMBOL}( ${ARGS_STR} );")
      else()
        message(FATAL_ERROR "Cannot parse \"${DECL}\"")
      endif()
    endforeach()

    head("")
    head("#ifdef __cplusplus")
    head("}")
    head("#endif")
    head("")
    head("\n#endif")

    link("}")

    file_write_changes(${CMAKE_CURRENT_LIST_DIR}/module.link.cpp "${LINK_SRC}")
    file_write_changes(${CMAKE_CURRENT_LIST_DIR}/commands.h "${HEADER_SRC}")
  endif()
endmacro()

# add all modules in bb/
file(GLOB modules RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} bb/*)
foreach(module ${modules})
  # if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${module}/CMakeLists.txt)
    add_subdirectory(${module})
  # ENDIF()
endforeach()
